plugins {
    id "java"
    id "application"
    id "edu.sc.seis.launch4j" version "3.0.6"
    id "org.openjfx.javafxplugin" version "0.1.0"
    id "com.gradleup.shadow" version "8.3.5"
}

group = ss_group
version = "5.0.0"

repositories {
    mavenCentral()
}

dependencies {
    implementation project(":build-shared")  // This makes shared classes available

    implementation "com.eclipsesource.minimal-json:minimal-json:0.9.5"
    implementation "info.picocli:picocli:4.7.5"
    annotationProcessor "info.picocli:picocli-codegen:4.7.5"
    implementation fileTree(dir: "libs", include: "*.jar")

    //Dependencies (GUI)
    implementation "org.openjfx:javafx-controls:$javafx.version:win"
    implementation "org.openjfx:javafx-controls:$javafx.version:linux"
    implementation "org.openjfx:javafx-controls:$javafx.version:mac"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(java_language_version)
    }
}

application {
    mainClass = ss_main_class
}

javafx {
    version = "17.0.13"
    modules = ["javafx.controls"]
}

jar {
    enabled = false
}

// Configure the shadowJar task (with JavaFX)
shadowJar {
    archiveBaseName = "server-sync"
    archiveClassifier = "client"

    //Don't minimize with JavaFX
    //minimize() //Comment to build GUI

    processResources {
        exclude "css/application.css"
    }

    manifest {
        attributes "Main-Class": ss_main_class
        attributes "Implementation-Version": project.version
    }

    dependencies {
        exclude "forge*.jar"
        //exclude '**/*javafx*/**' //Comment to build GUI
    }
}

launch4j {
    jarTask = project.tasks.shadowJar
    mainClassName = ss_main_class
    outfile = "ServerSyncClient.exe"
    icon = "${rootDir}/build-shared/src/main/resources/ServersyncLogo.ico"
    copyConfigurable = []
    supportUrl = "https://github.com/superzanti/ServerSync"
    productName = "ServerSync - Client"
    fileDescription = "The client side for users."
    version = project.version
}

startScripts {
    dependsOn shadowJar
    classpath = files(classpath = files("${layout.buildDirectory.get()}/libs/${project.name}-${project.version}.jar"))
}


distZip {
    dependsOn shadowJar
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

distTar {
    dependsOn shadowJar
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

distributions {
    main {
        contents {
            from(shadowJar)
        }
    }
}

tasks.register("assembleGithubArtifacts", Copy) {
    group = "distribution"
    dependsOn createExe
    from layout.buildDirectory.dir("libs"), layout.buildDirectory.file("launch4j")
    into file("$rootDir/publish/github")
    exclude "lib"
}

clean {
    delete "$rootDir/publish"
}

